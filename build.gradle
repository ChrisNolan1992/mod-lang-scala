/*
 * Copyright 2012 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'eclipse'
apply from: 'gradle/maven.gradle'
apply from: 'gradle/setup.gradle'

defaultTasks = ['assemble']

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

repositories {
  mavenLocal()
  maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
//  maven { url 'http://repo.springsource.org/release' }
  mavenCentral()
}

configurations {
  provided
}

sourceSets {
  main {
    compileClasspath = compileClasspath + configurations.provided
  }
}

dependencies {
  scalaTools "org.scala-lang:scala-compiler:$scalaVersion"
  scalaTools "org.scala-lang:scala-library:$scalaVersion"

  compile "org.vert-x:vertx-core:$vertxVersion"
  compile "org.vert-x:vertx-platform:$vertxVersion"
  compile "org.scala-lang:scala-library:$scalaVersion"
  compile "org.scala-lang:scala-compiler:$scalaVersion"

  testCompile "junit:junit:$junitVersion"

  testCompile "org.vert-x:vertx-junit-annotations:$junitAnnoVersion"
  testCompile "org.vert-x:vertx-lang-java:$vertxVersion"
  testCompile "org.vert-x:vertx-lang-rhino:$vertxVersion"
  testCompile "org.mozilla:rhino:$rhinoVersion"
  testCompile("org.vert-x:vertx-testframework:$vertxVersion") {
    transitive = false // don't need all of the other language deps
  }
}

compileScala {
//  scalaCompileOptions.additionalParameters = ['-target=jvm-1.7']
//  scalaCompileOptions.sourceCompatibility = project.sourceCompatibility
  scalaCompileOptions.targetCompatibility = project.targetCompatibility
//  scalaCompileOptions.useCompileDaemon = true
//  scalaCompileOptions.daemonServer = "localhost:4243"
}

javadoc.classpath += configurations.provided

test {
  // Some vert.x properties
  systemProperty 'vertx.test.timeout', 15
  systemProperty 'vertx.langs.scala', 'org.vertx.scala.deploy.ScalaVerticleFactory'
  systemProperty 'vertx.mods', "$projectDir/build/tmp/mod-test"
  systemProperty 'vertx.version', "$project.version"

  // Show output
  testLogging.showStandardStreams = true
}

task collectDeps(type: Copy) {
  group = 'vert.x'
  description = 'conveniently collect dependencies for other IDEs'
  destinationDir = file("build/deps")
  into("compile") {
    from configurations.compile
  }
  into("test") {
    from configurations.testCompile
  }
}

task prepareDist(type:Sync, dependsOn: ['build']) {
  group = 'vert.x'
  description = 'Prepare distribution'
  destinationDir file("build/${project.name}-v${project.version}")
  from jar
  from configurations.compile
}

task dist(type:Zip, dependsOn: ['prepareDist']) {
  group = 'vert.x'
  description = 'Package distribution'
  destinationDir file("build/distributions")
  from "build/${project.name}-v${project.version}"
  into 'build/distributions'
}

